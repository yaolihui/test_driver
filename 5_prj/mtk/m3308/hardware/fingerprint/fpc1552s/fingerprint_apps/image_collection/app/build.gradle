apply plugin: 'com.android.application'

android {
    compileSdkVersion 28

    defaultConfig {
        archivesBaseName = "ImageCollection"
        minSdkVersion 26
        targetSdkVersion 28
    }

    signingConfigs {
        release {
            storeFile file("../systemkeystore.keystore")
            storePassword "fpcstore"
            keyAlias "fingerprint"
            keyPassword "fpc201608"
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.release
        }

        release {
            signingConfig signingConfigs.release
        }
    }

    lintOptions {
        // Don't abort if Lint finds an error, otherwise the Jenkins build
        // will be marked as failed, and Jenkins won't analyse the Lint output
        abortOnError false
    }
    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }
}

repositories {
    flatDir {
        dirs 'libs'
    }
}

configurations.all {
    // Check for updates every build
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

dependencies {
    if (findProject(":fingerprint_extension") != null) {
        implementation project(":fingerprint_extension")
    }
    if (findProject(":fmi") != null) {
        api project(':fmi')
    }

    compileOnly files(System.getProperty("user.dir") + '../../../fingerprint_extension/api/generated/android.hidl.base-V1.0.jar');
    compileOnly files(System.getProperty("user.dir") + '../../../fingerprint_extension/api/generated/com.fingerprints.extension-V1.0.jar');

    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:support-annotations:28.0.0'
    implementation 'com.android.support:support-v4:28.0.0'

    api(name: 'gson-2.7', ext: 'jar')
    api(name: 'xmltools-release', ext: 'aar')
}
